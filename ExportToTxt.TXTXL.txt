Representación Textual del archivo de Excel: "ExportToTxt.xls"

Hojas:
 [Sheet1]

Contenido:
[Sheet1]

Nombres:

Nombre del proyecto VB: [Lib_ExportToTxt]

Módulos VBA:
 [ThisWorkbook] Tipo: 100
 [Sheet1] Tipo: 100
 [ExportToTxt] Tipo: 1
 [ExportToTxtClass] Tipo: 2

Codigo:
[ThisWorkbook] 12 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

Private Sub Workbook_BeforeSave(ByVal SaveAsUI As Boolean, Cancel As Boolean)

    'En caso de que se grabe éste libro, que es el que tiene el código, vamos a llamar directamente a la rutina:
    ExportToTxt.ExportToTxt Me

    'Para los demás libros, se debe de mandar llamar la rutina que dispare el proceso:
    'On Error Resume Next: Application.Run "Personal.xlsb!ExportToTxt.ExportToTxt", Me

End Sub

\\\--- END OF MODULE ---

[Sheet1] 0 líneas de código.
///--- BEGINNING OF MODULE ---
\\\--- END OF MODULE ---

[ExportToTxt] 316 líneas de código.
///--- BEGINNING OF MODULE ---
'---------------------------------------------------------------------------------------
' Module    : ExportToTxt
' Author    : +
' Date      : 05/06/2013
' Purpose   : Exporta una representación textual de un libro de Excel
' Version   : v2.5 - 2013-06-07.
'---------------------------------------------------------------------------------------

Option Explicit

Private Const TOKEN_PRE_EXTENSION = ".TXTXL"
Private Const TOKEN_EXTENSION = ".txt"

Private Const TOKEN_HEADER = "Representación Textual del archivo de Excel: "
Private Const TOKEN_HOJAS_TITLE = "Hojas:"
Private Const TOKEN_CONTENT_TITLE = "Contenido:"
Private Const TOKEN_NOMBRES_TITLE = "Nombres:"
Private Const TOKEN_MODULOS_TITLE = "Módulos VBA:"
Private Const TOKEN_CODIGO_TITLE = "Codigo:"
Private Const TOKEN_MODULE_BEGINS = "///--- BEGINNING OF MODULE ---"
Private Const TOKEN_MODULE_END = "\\\--- END OF MODULE ---"
Private Const SHEET_TITLE_CELL_LISTING_THRESHOLD = 25 'El número de celdas entre la etiqueta de la hoja en el listado de celdas

Private FSO As New Scripting.FileSystemObject
Private Stream As TextStream 'El stream donde vamos a escribir el archivo

Private BTE As Workbook '(BookToExport) Referencia al libro que vamos a exportar.

Private ErrNum, ErrDesc 'Auxiliares para mantener los detalles del error si ocurre durante la ejecución de un delegado.

Sub ExportToTxtConConfirmacion(bookToExport As Workbook)
    Select Case MsgBox("Inicio del proceso de exportación a texto." _
                       & vbCrLf & "Dependiento del tamaño y complejidad del libro, puede tardar algunos minutos." _
                       & vbCrLf & "" _
                       , vbOKCancel Or vbInformation Or vbDefaultButton1, "Exportación a Texto")
    Case vbCancel
        Call MsgBox("Proceso cancelado.", vbInformation Or vbDefaultButton1, "Exportación a Texto")
        Exit Sub
    End Select
    
    ExportToTxt bookToExport
    
    Call MsgBox("Proceso terminado.", vbInformation Or vbDefaultButton1, "Exportación a Texto")
End Sub
Sub ExportToTxt(bookToExport As Workbook)
    
On Error GoTo Catch
    Set BTE = bookToExport
    Set Stream = FSO.CreateTextFile(GetTXTFileName(), True, False)
    
    GenEncabezado
    GenHojas
    GenNombres
    GenModulos
    
    Log "** Fin del proceso de exportación **"
    
Catch:
    If Err.Number <> 0 Then
        Dim MensajeDeError As String
        MensajeDeError = "Ocurrió un error al correr el proceso de exportación" & vbCrLf & "El error reportado es: " & vbCrLf & Err.Number & " " & Err.Description '& vbCrLf & Err.Source
        MsgBox MensajeDeError
        If Not StreamClosed() Then Log MensajeDeError
    End If
    Stream.Close
End Sub

Function GetTXTFileName() As String
    Dim NombreDelArchivo As String _
    , Ruta As String _
    , NombreCompleto As String
    
    NombreDelArchivo = FSO.GetBaseName(BTE.name) & TOKEN_PRE_EXTENSION & TOKEN_EXTENSION
    Ruta = BTE.Path
    NombreCompleto = FSO.BuildPath(Ruta, NombreDelArchivo)
    
    GetTXTFileName = NombreCompleto
End Function

Sub GenEncabezado()
Log TOKEN_HEADER & """" & BTE.name & """"
Log
End Sub

Sub GenHojas()
'--- Lista de hojas
Log TOKEN_HOJAS_TITLE
IteraHojas "ImprimeNombreDeHoja"
Log

'--- Contenido de cada hoja
Log TOKEN_CONTENT_TITLE
IteraHojas "ProcesaContenidoDeHoja"
End Sub

Sub GenNombres()
Log TOKEN_NOMBRES_TITLE
IteraNombres "ImprimeNombre"
Log
End Sub

Sub GenModulos()
    Log "Nombre del proyecto VB: [" & BTE.VBProject.name & "]"
    Log
    
    'Antes de intentar acceder a los módulos, verificamos que el proyecto no esté protegido:
    If BTE.VBProject.Protection <> vbext_pp_locked Then
        Log TOKEN_MODULOS_TITLE
        IteraModulos "ImprimeNombreDeModulo"
        Log
        
        Log TOKEN_CODIGO_TITLE
        IteraModulos "ProcesaModulo"
        Log
    Else
        Log "** No se pueden exportar las macros si el proyecto está protegido. **"
    End If
End Sub

'Delegate
Sub ProcesaModulo(vbcomp As vbcomponent, iterationIndex As Integer)
On Error GoTo DelegateError
    Dim CodeLines As Integer
    CodeLines = vbcomp.CodeModule.CountOfLines
    Log "[" & vbcomp.name & "] " & CodeLines & " líneas de código."
    
    Log TOKEN_MODULE_BEGINS
    If CodeLines > 0 Then
        Log vbcomp.CodeModule.Lines(1, CodeLines)
    End If
    Log TOKEN_MODULE_END
    
    Log
DelegateError:
    ErrNum = Err.Number: ErrDesc = Err.Description
End Sub

'Delegate
Sub ImprimeNombreDeModulo(vbcomp As vbcomponent, iterationIndex As Integer)
On Error GoTo DelegateError
    Log " [" & vbcomp.name & "] Tipo: " & vbcomp.Type
DelegateError:
    ErrNum = Err.Number: ErrDesc = Err.Description
End Sub

'Delegate caller
Sub IteraModulos(DelegateName As String)
    Dim vbcomponent As vbcomponent
    Dim ix As Integer
    ix = 0
    On Error GoTo ErrorAccesandoVBProject
    For Each vbcomponent In BTE.VBProject.VBComponents
        ix = ix + 1
        ErrNum = 0: Application.Run DelegateName, vbcomponent, ix
        If ErrNum <> 0 Then Err.Raise ErrNum, , ErrDesc
    Next
ErrorAccesandoVBProject: 'Si truena no hacemos nada.
    Exit Sub
End Sub

'Delegate
Sub ImprimeNombre(name As name, iterationIndex As Integer)
On Error GoTo DelegateError
    Log " [" & name.name & "] " & name.RefersTo
DelegateError:
    ErrNum = Err.Number: ErrDesc = Err.Description
End Sub

'Delegate caller
Sub IteraNombres(DelegateName As String)
    Dim name As name
    Dim ix As Integer
    ix = 0
    For Each name In BTE.Names
        ix = ix + 1
        ErrNum = 0: Application.Run DelegateName, name, ix
        If ErrNum <> 0 Then Err.Raise ErrNum, , ErrDesc
    Next
End Sub

'Delegate
Sub ImprimeNombreDeHoja(sheet As Worksheet, iterationIndex As Integer)
On Error GoTo DelegateError
    Dim Mensaje As String
    If Not sheet.Visible Then Mensaje = Mensaje & " - Escondida"
    If sheet.ProtectContents Then Mensaje = Mensaje & " - Protegida"
    Log " [" & sheet.name & "]" & Mensaje
DelegateError:
    ErrNum = Err.Number: ErrDesc = Err.Description
End Sub

'Delegate
Sub ProcesaContenidoDeHoja(sheet As Worksheet, iterationIndex As Integer)
On Error GoTo DelegateError
    'Determinamos si la hoja está protegida y hay que desprotegerla.
    Dim Mensaje As String
    Dim BkHojaProtegida As Boolean
    If sheet.ProtectContents Then
        BkHojaProtegida = True
        Mensaje = Mensaje & " - Hoja Protegida"
        
        'La intentamos desproteger
        On Error Resume Next: sheet.Unprotect " "
        
        If Not sheet.ProtectContents Then
            Mensaje = Mensaje & " - Se desprotegió sin contraseña."
        Else
            Mensaje = Mensaje & " - NO SE DESPROTEGIÓ."
        End If
    End If
    
    'Procesamos las celdas.
    Log "[" & sheet.name & "]" & Mensaje
    IteraCeldas sheet, "ProcesaCelda"
    Log
    
    'Reprotegemos si es necesario
    If BkHojaProtegida Then sheet.Protect
DelegateError:
    ErrNum = Err.Number: ErrDesc = Err.Description
End Sub

'Delegate caller
Sub IteraHojas(DelegateName As String)
    Dim sheet As Worksheet
    Dim ix As Integer
    ix = 0
    For Each sheet In BTE.Worksheets
        ix = ix + 1
        ErrNum = 0: Application.Run DelegateName, sheet, ix
        If ErrNum <> 0 Then Err.Raise ErrNum, , ErrDesc
    Next
End Sub

'Delegate
Function ProcesaCelda(cell As Range, iterationIndex As Integer) As Integer
On Error GoTo DelegateError
    ProcesaCelda = iterationIndex 'Inmediatamente establecemos el valor
    
    Dim Direccion As String
    Dim Contenido As String
    Dim NombreDefinido As String
    Dim CeldaSeProcesa As Boolean
    
    If cell.FormulaHidden And cell.Parent.ProtectContents Then 'Fórmula escondida y la hoja está protegida.
        CeldaSeProcesa = True
        Contenido = "**FÓRMULA INACCESIBLE**"
    ElseIf cell.Formula <> "" Then 'Fórmula accesible, pero que no esté vacía.
        CeldaSeProcesa = True
        Contenido = cell.Formula
    End If
    
    If CeldaSeProcesa Then
        'Insertamos el nombre de la hoja cada N celdas
        If iterationIndex Mod SHEET_TITLE_CELL_LISTING_THRESHOLD = 0 And iterationIndex <> 0 Then
            Log "[" & cell.Parent.name & "]"
        End If
        Direccion = cell.Address(False, False): Direccion = "[" & Direccion & "] "
        NombreDefinido = GetNameOfCell(cell): If NombreDefinido <> "" Then NombreDefinido = "(" & NombreDefinido & ") "
        Contenido = "\" & Contenido & "\"
        Log Direccion & NombreDefinido & Contenido
        ProcesaCelda = iterationIndex + 1
    End If
DelegateError:
    ErrNum = Err.Number: ErrDesc = Err.Description
End Function

Function GetNameOfCell(cell As Range) As String
    On Error GoTo NoName
    GetNameOfCell = cell.name.name
Exit Function
NoName:
    GetNameOfCell = ""
End Function

'Delegate caller
Sub IteraCeldas(sheet As Worksheet, DelegateName As String)
    Dim cell As Range, column As Range
    Dim ix As Integer
    ix = 0
    For Each column In sheet.UsedRange.Cells.Columns
        For Each cell In column.Cells
            ErrNum = 0: ix = Application.Run(DelegateName, cell, ix)
            If ErrNum <> 0 Then Err.Raise ErrNum, , ErrDesc
        Next
        DoEvents
    Next
End Sub

Sub Log(Optional Mensaje As String)
'Debug.Print Mensaje
Stream.WriteLine (Mensaje)
End Sub

Function StreamClosed() As Boolean
    On Error GoTo IsClosed
    StreamClosed = Stream.AtEndOfLine
    StreamClosed = False 'No marcó error, el Stream no está cerrado.
    Exit Function
IsClosed:
    StreamClosed = True
End Function

Sub PlantillaParaDelegado()
On Error GoTo DelegateError
    'Código aquí.
    'Si el delegado regresa valor, es buena idea asignarlo de inmediato, si se tiene un valor default.
DelegateError:
    ErrNum = Err.Number: ErrDesc = Err.Description
End Sub

Sub PlantillaParaLlamarADelegado()
    'ErrNum y ErrDesc deben de estar definidas a nivel módulo.
    ErrNum = 0: Application.Run "NombreDelDelegado" ',Más, parámetros, etc
    If ErrNum <> 0 Then Err.Raise ErrNum, , ErrDesc
End Sub
\\\--- END OF MODULE ---

[ExportToTxtClass] 2 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

\\\--- END OF MODULE ---


** Fin del proceso de exportación **
