Representación Textual del archivo de Excel: "PersonalMacroFileExample.xls"

Hojas:
 [Sheet1]

Contenido:
[Sheet1]

Nombres:

Nombre del proyecto VB: [VBAProject]

Módulos VBA:
 [ThisWorkbook] Tipo: 100
 [Sheet1] Tipo: 100
 [MyLibs] Tipo: 1
 [BookLoader] Tipo: 1

Codigo:
[ThisWorkbook] 6 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

Private Sub Workbook_BeforeSave(ByVal SaveAsUI As Boolean, Cancel As Boolean)
MyLibs.ExportToTxt Me
End Sub

\\\--- END OF MODULE ---

[Sheet1] 0 líneas de código.
///--- BEGINNING OF MODULE ---
\\\--- END OF MODULE ---

[MyLibs] 71 líneas de código.
///--- BEGINNING OF MODULE ---
'---------------------------------------------------------------------------------------
' Module    : MyLibs
' Author    : +
' Date      : 04/06/2013
' Purpose   : Módulo que va a servir de pegamento entre este archivo de Macros Personales y los diferentes archivos con librerías.
'---------------------------------------------------------------------------------------

Option Explicit

'====================================================================================================
' Uso de ExportToTxt
'====================================================================================================
'En los libros que se quiera exportar a texto, agregar el siguiente estatuto en su evento Workbook.BeforeSave:
'Application.Run "Personal.xlsb!MyLibs.ExportToTxt", Me

'Si se quiere hacer en demanda, usar esta línea en una rutina nueva en un módulo estándar:
'Application.Run "Personal.xlsb!MyLibs.ExportToTxtConConfirmación", Me
'====================================================================================================

'Ejemplo de la rutina (Y el módulo) que debe de estar presente en el Archivo de Macros Personales para correr la funcionalidad.
Public Sub ExportToTxt(book As Workbook)

    On Error Resume Next 'Estos dos estatutos pueden fallar si todos los libros abiertos en excel están ocultos sin ventanas en Excel:
    Dim BkCalculationMode: BkCalculationMode = Application.Calculation
    Application.Calculation = xlCalculationManual
    
    On Error GoTo ExportToTxt_Error
    BookLoader.OpenBook "..\ExportToTxt.xls"
    
    Application.Run "ExportToTxt.xls!ExportToTxt.ExportToTxt", book
    
    BookLoader.CloseBook
    On Error Resume Next: Application.Calculation = BkCalculationMode 'Puede fallar si no hay ventanas de libros en Excel. (O si falló al inicio de la rutina)
    
On Error GoTo 0
Exit Sub

ExportToTxt_Error:
    Debug.Print "*** Ocurrió un error en la rutina ExportToTxt.ExportToTxt"
    Debug.Print "*** Error No: " & Err.Number & ". " & Err.Description
    If Err.Number = 1004 Then 'Cachamos el error de apertura del archivo.
    Else 'Cachamos el resto de los errores. Asumimos que se abrió el archivo y el error fue en la llamada de Application.Run
        BookLoader.CloseBook
    End If
End Sub

Public Sub ExportToTxtConConfirmacion()

    On Error Resume Next 'Estos dos estatutos pueden fallar si todos los libros abiertos en excel están ocultos sin ventanas en Excel:
    Dim BkCalculationMode: BkCalculationMode = Application.Calculation
    Application.Calculation = xlCalculationManual
    
    On Error GoTo ExportToTxt_Error
    BookLoader.OpenBook "..\ExportToTxt.xls"
    
    Application.Run "ExportToTxt.xls!ExportToTxt.ExportToTxtConConfirmacion", ActiveWorkbook
    
    BookLoader.CloseBook
    On Error Resume Next: Application.Calculation = BkCalculationMode 'Puede fallar si no hay ventanas de libros en Excel. (O si falló al inicio de la rutina)
    
On Error GoTo 0
Exit Sub

ExportToTxt_Error:
    Debug.Print "*** Ocurrió un error en la rutina ExportToTxt.ExportToTxt"
    Debug.Print "*** Error No: " & Err.Number & ". " & Err.Description
    If Err.Number = 1004 Then 'Cachamos el error de apertura del archivo.
    Else 'Cachamos el resto de los errores. Asumimos que se abrió el archivo y el error fue en la llamada de Application.Run
        BookLoader.CloseBook
    End If
End Sub
\\\--- END OF MODULE ---

[BookLoader] 14 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

Private SummonedBook As Workbook

Public Sub OpenBook(bookFilenameRelativePath As String)
    Dim CurrentActiveBook As Workbook: Set CurrentActiveBook = ActiveWorkbook
    Set SummonedBook = Workbooks.Open(ThisWorkbook.Path & "\" & bookFilenameRelativePath)
    On Error Resume Next: CurrentActiveBook.Activate 'Puede fallar si el libro está oculto (en la ventana de Excel)
End Sub

Public Sub CloseBook()
    SummonedBook.Close False
    Set SummonedBook = Nothing
End Sub
\\\--- END OF MODULE ---


** Fin del proceso de exportación **
